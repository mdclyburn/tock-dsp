;;; -*- mode: asm -*-
;;;
;;; Single-channel I²S
;;;
;;; Drive a mono audio signal through the I²S left channel.
;;; This version clocks out the MSB of the word with the opposite channel's LRCLK.
;;; It takes two cycles to output one bit of a word.
;;;

    .program i2s_wclk_msb

    ;; bit 0 = BCLK, bit 1 = LRCLK
    .side_set 2

    .wrap_target
left_ch:
    set x, 15 side 0b01                           ; Set up counter. Cue on BCLK (LSB of right channel).
left_ch_loop:
    out pins, 1 side 0b00                         ; Write left channel bit.
    jmp x-- left_ch_loop side 0b01                ; Repeat for the first 15 bits. Cue on BCLK.
    out pins, 1 side 0b10                         ; Write last bit. Goes with the right channel's LRCLK.

right_ch:
    set x, 15 side 0b11                           ; Set up counter. Cue on BCLK (LSB of left channel).
right_ch_loop:
    set pins, 0 side 0b10                         ; Write empty right channel.
    jmp x--, right_ch_loop side 0b11              ; Repeat for the first 15 bits. Cue on BCLK.
    set pins, 0 side 0b00                         ; Write last bit. Goes with the left channel's LRCLK.
    .wrap
