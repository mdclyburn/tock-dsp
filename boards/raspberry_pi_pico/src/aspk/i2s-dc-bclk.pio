;;; -*- mode: asm -*-
;;;
;;; Single-channel I²S
;;;
;;; Drive a mono audio signal through the I²S left channel.
;;; This version clocks out one bit before the MSB of each channel.
;;; It takes two cycles to output one bit of a word.
;;;

    .program i2s_dc_bclk

    ;; bit 0 = BCLK, bit 1 = LRCLK
    .side_set 2

    .wrap_target
left_ch:
    set pins, 0 side 0b00                         ; Output known low value. Clock low for empty bit.
    set x, 15 side 0b01                           ; Set up counter. Cue on BCLK.
left_ch_loop:
    out pins, 1 side 0b00                         ; Write the left channel bit.
    jmp x-- left_ch_loop side 0b01                ; Repeat to output 16 bits. Cue on BCLK.

right_ch:
    set pins, 0 side 0b10                         ; Output known low value. Clock low for empty bit.
    set x, 15 side 0b11                           ; Set up counter. Cue on BCLK.
right_ch_loop:
    set pins, 1 side 0b10                         ; Write empty right channel.
    jmp x--, right_ch_loop side 0b11              ; Repeat to output 16 bits. Cue on BCLK.
    .wrap
